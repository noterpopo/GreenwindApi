plugins {
    id 'java'
}

group 'GreenWind'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'io.vertx:vertx-codegen:3.4.1'

    compile "io.vertx:vertx-core:3.4.1"
    compile 'io.vertx:vertx-web:3.4.1'
    compile 'io.vertx:vertx-mongo-client:3.4.1'

    testCompile 'io.vertx:vertx-unit:3.4.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task "create-dirs" << {
    sourceSets*.java.srcDirs*.each{
        it.mkdirs()
    }
    sourceSets*.resources.srcDirs*.each{
        it.mkdirs()
    }
}

task annotationProcessing(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-Acodegen.output=${destinationDir.absolutePath}"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

jar {
    // by default fat jar
    archiveName = 'greenwindserver-fat.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'verticle.SingleApplicationVerticle'
    }
}
